import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'idea'

    id "com.palantir.baseline-idea" version '4.185.0'
    id "com.diffplug.spotless" version '6.12.1'
    id "org.sonarqube" version '3.5.0.2730'
    id "org.owasp.dependencycheck" version '7.4.4'

    id 'net.nemerosa.versioning' version '3.0.0'

    id 'org.openapi.generator' version '6.5.0'
}

apply plugin: 'com.palantir.baseline-format'

repositories {
    mavenLocal()
    maven {
        credentials {
            username "$nexusUsername"
            password "$nexusPassword"
        }
        url "$nexusRelease"
    }
    maven {
        credentials {
            username "$nexusUsername"
            password "$nexusPassword"
        }
        url "$nexusSnapshot"
    }
    mavenCentral()
}

dependencies {
    implementation 'io.swagger:swagger-annotations:1.6.8'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'

    // test
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

java {
    withSourcesJar()
}
jar {
    manifest {
        attributes(
                'Built-By': 'Codingendless',
                'Build-Timestamp': DateTimeFormatter.ofPattern("yyyy-MM-dd HH:mm:ss").format(LocalDateTime.now()),
                'Build-Revision': versioning.info.commit,
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

plugins.withType(JavaPlugin) {
    project.sourceCompatibility = JavaVersion.VERSION_11
}
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

test {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = 'java'
    library = 'native'
    inputSpec = "$rootDir/openai-openapi/openapi.yaml".toString()
    outputDir = "$buildDir/gen/openapi".toString()
    apiPackage = 'com.fortycoderplus.openai.sdk.api'
    invokerPackage = 'com.fortycoderplus.openai.sdk.invoker'
    modelPackage = 'com.fortycoderplus.openai.sdk.model'
    configOptions = [
            dateLibrary: 'java8'
    ]
    skipValidateSpec = true
}

sourceSets.main.java.srcDirs += ["$buildDir/gen/openapi/src/main/java"]

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = project.name
            version = project.version

            from components.java

            pom {
                name = "OpenAI API Java SDK"
                description = 'OpenAI API Java SDK'
                url = "${scmUrl}"
                developers {
                    developer {
                        id = 'Codingendless'
                        name = 'Codingendless'
                        email = 'codingendless@40coderplus.com'
                    }
                }
                scm {
                    connection = "scm:git:${scmGit}"
                    url = "${scmUrl}"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username "$nexusUsername"
                password "$nexusPassword"
            }
            url = version.endsWith('SNAPSHOT') ? "$nexusSnapshot" : "$nexusRelease"
            allowInsecureProtocol = !url.toString().startsWith("https://")
        }
    }
}

sonarqube {
    properties {
        property 'sonar.dependencyCheck.jsonReportPath', 'build/reports/dependency-check-report.json'
        property 'sonar.dependencyCheck.htmlReportPath', 'build/reports/dependency-check-report.html'
        property 'sonar.projectName', project.name
        property 'sonar.projectKey', project.name
    }
}

spotless {
    java {
        target 'src/main/java/**/*.java', 'src/test/java/**/*.java'

        palantirJavaFormat()
        formatAnnotations()
    }
}